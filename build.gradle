//项目地址  https://github.com/ggggxiaolong/Hencode
apply plugin: 'com.github.ben-manes.versions'
import org.ajoberstar.grgit.Grgit

buildscript {
  apply from: "dependencies/versions.gradle"

  addRepos(repositories)
  dependencies {
    classpath deps.android_gradle_plugin
    classpath deps.grgit
    //检查版本更新  ./gradlew dependencyUpdates -Drevision=release
    classpath deps.lib_versions
    classpath deps.kotlin.plugin
    classpath deps.kotlin.android_extensions
  }
}

allprojects {
  def git = Grgit.open(currentDir: projectDir)
  addGitVersion(git.describe(), git.tag.list().size())

  addRepos(repositories)
}

subprojects {
  def isAppModule = isModule.toBoolean() || it.name.equals("app")
  def dataBindingModules = ["app"]
  //使用dataBinding的模块
  def enableDataBinding = dataBindingModules.contains(it.name)

  apply plugin: isAppModule ? 'com.android.application' : 'com.android.library'

  apply plugin: 'kotlin-android'
  apply plugin: 'kotlin-android-extensions' // 拓展

  apply plugin: 'kotlin-kapt'

  android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
      applicationId "com.mrtan.hencode"
      minSdkVersion build_versions.min_sdk
      targetSdkVersion build_versions.target_sdk

      versionCode build_versions.androidVersionCode
      versionName build_versions.androidVersionName
      testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
      multiDexEnabled true
      vectorDrawables.useSupportLibrary = true //drawable支持

      //arouter
      //      javaCompileOptions {
      //        annotationProcessorOptions {
      //          arguments = [ moduleName : project.getName() ]
      //        }
      //      }
    }
    buildTypes {
      debug {
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
        minifyEnabled true
        zipAlignEnabled true
        jniDebuggable true
      }
      release {
        minifyEnabled true
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      }
    }

    //    flavorDimensions "mode"
    //    productFlavors {
    //      dev {
    //        applicationIdSuffix ".dev"
    //      }
    //      prod {
    //      }
    //    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    if (enableDataBinding) {
      dataBinding {
        enabled true
      }
    }
  }

  dependencies {
    implementation deps.kotlin.stdlib

    implementation deps.rxjava2
    implementation deps.rx_android
    debugImplementation deps.leackCanary
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
